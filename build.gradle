plugins {
    id 'java'
    id 'jacoco'

    id 'maven-publish'
    id 'distribution'

    id 'com.github.hierynomus.license' version '0.16.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.github.ben-manes.versions" version "0.51.0"
    id "org.sonarqube" version "4.4.1.3373"
}

def envDB
// sys property (-Denv) or env var (ORG_GRADLE_PROJECT_env) as a fallback
def isTravis = System.getProperty('env', env) != null && System.getProperty('env', env)?.equals('travis')
if (isTravis) {
  // envDB = 'PostgreSQL,MySQL,H2,HSQLDB,SQLite,Firebird,Derby,Oracle,MSSQLServer,MariaDB,Percona'
  envDB = 'PostgreSQL,MySQL,H2,HSQLDB,SQLite,Firebird,Derby,Oracle'
} else {
  // sys property (-DDB) or env var (ORG_GRADLE_PROJECT_DB) as a fallback
  envDB = System.getProperty('DB', DB)
}

repositories {
    mavenCentral()
    // Additional repository for older plugin dependencies
    maven {
        url 'https://repo1.maven.org/maven2'
    }
    flatDir dirs: 'src/test/repo' // holds p6spy-signedjar-test

    if (envDB.contains('Oracle')) {
      assert new File("${buildDir}/repo/ojdbc6.jar").exists()
      flatDir dirs: "${buildDir}/repo" // holds copied oracle jdbc driver
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileTestJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

compileJava.options.encoding = 'UTF8'
javadoc.options.encoding = 'UTF8'

configurations {
    compileOnlyDeps {
        canBeResolved = true
        extendsFrom compileOnly
    }
}

dependencies {

  testImplementation 'p6spy:p6spy-signedjar-test:1.0.0'

  // for providing misc *-nodep artifacts, please note that order matters here
  // even if slf4j complains about multiple implementations on classpath, it takes the 1.st one
  // and as we use log4j in tests => bridge needs to be the 1.st one specified
  compileOnly 'org.slf4j:slf4j-api:1.7.25'
  compileOnly 'org.slf4j:slf4j-log4j12:1.7.25'
  compileOnly 'log4j:log4j:1.2.17'
  testImplementation 'org.slf4j:slf4j-api:1.7.25'
  testImplementation 'org.slf4j:slf4j-log4j12:1.7.25'
  testImplementation 'log4j:log4j:1.2.17'

  compileOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.1'
  compileOnly 'org.apache.logging.log4j:log4j-api:2.17.1'
  compileOnly 'org.apache.logging.log4j:log4j-core:2.17.1'

  compileOnly 'ch.qos.logback:logback-classic:1.2.3'
  compileOnly 'ch.qos.logback:logback-core:1.2.3'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.eclipse.jetty:jetty-plus:9.4.12.v20180830' // datasource testing through JNDI
  testImplementation 'commons-dbcp:commons-dbcp:1.4' // datasource testing
  testImplementation 'org.codehaus.btm:btm:2.1.4' // xa datasource testing
  testImplementation 'org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1'
  testImplementation 'org.liquibase:liquibase-core:3.0.8'
  testImplementation 'com.mattbertolini:liquibase-slf4j:1.2.1'
  testImplementation 'org.springframework:spring-jdbc:5.1.0.RELEASE'
  testImplementation 'org.springframework:spring-context:5.1.0.RELEASE'
  testImplementation 'com.j256.simplejmx:simplejmx:1.15'
  testImplementation 'commons-io:commons-io:2.6'
  testImplementation 'org.apache.commons:commons-exec:1.3'
  testImplementation 'org.apache.commons:commons-lang3:3.8.1'
  testImplementation 'commons-beanutils:commons-beanutils:1.9.3'
  testImplementation 'org.mockito:mockito-core:2.22.0'
  testImplementation 'org.apache.tomcat:tomcat-jdbc:9.0.12' // tomcat pooled datasource testing

  // all the JDBC drivers tested
  testRuntimeOnly 'mysql:mysql-connector-java:5.1.47'
  testRuntimeOnly 'org.postgresql:postgresql:42.2.5.jre7'
  testImplementation 'org.hsqldb:hsqldb:2.4.1'
  testImplementation 'com.h2database:h2:1.4.197'
  testRuntimeOnly 'org.xerial:sqlite-jdbc:3.25.2' // type 3 driver
  testRuntimeOnly 'org.apache.derby:derby:10.14.2.0'
  testRuntimeOnly 'org.firebirdsql.jdbc:jaybird-jdk17:3.0.5' // type 4.1 driver
  testRuntimeOnly 'com.microsoft.sqlserver:mssql-jdbc:7.1.1.jre8-preview'

  if (envDB.contains('Oracle')) {
    testRuntimeOnly name: 'ojdbc6' // oracle copied from docker oracle container
  }
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
    }
}

license {
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'P6Spy'
    header file('./script/qa/license_header.txt')
}

test {
    systemProperty 'user.language', 'en'
    systemProperty 'user.country', 'US'
    systemProperty 'derby.stream.error.file', 'target/derby.log'
    systemProperty 'DB', envDB

    // to enable remote JMX testing,
    // see: http://stackoverflow.com/questions/5552960/how-to-connect-to-a-java-program-on-localhost-jvm-using-jmx
    // to workaround oracle timezone issue: http://stackoverflow.com/questions/9156379/ora-01882-timezone-region-not-found
    // to get rid of DB2 traces => -Ddb2.jcc.override.traceLevel=0
    // see: http://publib.boulder.ibm.com/infocenter/idshelp/v111/index.jsp?topic=/com.ibm.jccids.doc/com.ibm.db2.luw.apdv.java.doc/doc/r0052075.htm -->
    jvmArgs '-Dcom.sun.management.jmxremote', '-Dcom.sun.management.jmxremote.authenticate=false', '-Dcom.sun.management.jmxremote.port=1234', '-Dcom.sun.management.jmxremote.ssl=false', '-Doracle.jdbc.timezoneAsRegion=false', '-Ddb2.jcc.override.traceLevel=0'
}

jar {
  inputs.property('moduleName', project.name)
  manifest {
    attributes('Automatic-Module-Name': project.name)
  }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task shadowLog4jJar(type: ShadowJar) {
  archiveClassifier.set('log4j-nodep')
  from sourceSets.main.output //tells the task to include the project code
  configurations = [ project.configurations.compileOnlyDeps ]
  dependencies {
    include(dependency( group: 'log4j', name: 'log4j'))
    include(dependency( group: 'org.slf4j', name: 'slf4j-api'))
    include(dependency( group: 'org.slf4j', name: 'slf4j-log4j'))
  }
}

task shadowLog4j2Jar(type: ShadowJar) {
  archiveClassifier.set('log4j2-nodep')
  from sourceSets.main.output //tells the task to include the project code
  configurations = [ project.configurations.compileOnlyDeps ]
  dependencies {
    include(dependency( group: 'org.slf4j', name: 'slf4j-api'))
    include(dependency( group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl'))
    include(dependency( group: 'org.apache.logging.log4j', name: 'log4j-api'))
    include(dependency( group: 'org.apache.logging.log4j', name: 'log4j-core'))
  }
}

task shadowLogbackJar(type: ShadowJar) {
  archiveClassifier.set('logback-nodep')
  from sourceSets.main.output //tells the task to include the project code
  configurations = [ project.configurations.compileOnlyDeps ]
  dependencies {
    include(dependency( group: 'org.slf4j', name: 'slf4j-api'))
    include(dependency( group: 'ch.qos.logback', name: 'logback-classic'))
    include(dependency( group: 'ch.qos.logback', name: 'logback-core'))
  }
}

distributions {
    main {
        contents {
            from { 'src/main/assembly/individualFiles' }
            from { 'license.txt' }
            from jar
            from javadocJar
            from sourcesJar
            from shadowLog4jJar
            from shadowLog4j2Jar
            from shadowLogbackJar
        }
    }
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			groupId project.group
      artifactId project.archivesBaseName
      version project.version

      artifact sourcesJar
      artifact javadocJar
      artifact shadowLog4jJar
      artifact shadowLog4j2Jar
      artifact shadowLogbackJar
      artifact distZip
      artifact distTar

      pom {
        packaging 'jar'
        withXml {
          asNode().children().last() + {
            def builder = delegate

            // maven central publishing mandatories
            builder.name project.name
            builder.description description
            builder.url 'https://github.com/p6spy/p6spy'

            builder.licenses {
                builder.license {
                  builder.name 'The Apache Software License, Version 2.0'
                  builder.url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                  builder.distribution 'repo'
                }
            }
            builder.scm {
                builder.url 'https://github.com/p6spy/p6spy'
                builder.connection 'scm:git:git://github.com/p6spy/p6spy.git'
                builder.developerConnection 'scm:git:ssh://github.com:p6spy/p6spy.git'
            }
            builder.developers {
                builder.developer {
                  builder.name 'Quinton McCombs'
                  builder.email 'quinton.mccombs@gmail.com'
                }
                builder.developer {
                  builder.name 'Peter Butkovic'
                  builder.email 'butkovic@gmail.com'
                }
                builder.developer {
                  builder.name 'Felix Barnsteiner'
                  builder.email 'felix.barnsteiner@isys-software.de'
                }
              builder.developer {
                builder.name 'Arthur Gavlyukovskiy'
                builder.email 'agavlyukovskiy@gmail.com'
              }
            }

            // maven central publishing optionals
            builder.issueManagement {
              builder.system 'github'
              builder.url 'https://github.com/p6spy/p6spy/issues'
            }
            builder.ciManagement {
              builder.system 'Travis CI'
              builder.url 'https://travis-ci.org/p6spy/p6spy'
            }
          }
        }
      }
		}
	}
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

tasks {
  check {
    dependsOn(jacocoTestReport)
  }
}
